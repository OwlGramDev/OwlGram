import java.nio.file.Paths

plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.huawei.agconnect'
}

configurations.all {
    exclude group: 'com.google.firebase', module: 'firebase-core'
    exclude group: 'androidx.recyclerview', module: 'recyclerview'
}

dependencies {
    implementation project(':TMessagesProj')
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.0'
    implementation files('../TMessagesProj/libs/libgsaverification-client.aar')
    implementation 'com.google.firebase:firebase-messaging:23.0.7'
    implementation 'com.huawei.hms:push:6.5.0.300'
    implementation 'com.huawei.hms:maps:6.6.0.300'
    implementation 'com.huawei.hms:location:6.4.0.300'
}

ext.abiCodes = ['universal': 0, 'armeabi-v7a': 1, 'arm64-v8a': 5, x86: 2, x86_64: 6]

android {
    defaultConfig.applicationId = 'org.telegram.messenger'
    compileSdkVersion 32
    buildToolsVersion '32.1.0-rc1'
    ndkVersion '21.4.7075529'
    sourceSets.main.jniLibs.srcDirs = ['../TMessagesProj/jni/']

    buildFeatures {
        prefab true
    }

    lint {
        checkReleaseBuilds false
        disable 'MissingTranslation', 'ExtraTranslation', 'BlockedPrivateApi'
    }

    packagingOptions {
        resources {
            excludes += '**'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11

        coreLibraryDesugaringEnabled true
    }

    Properties signingProperties = new Properties()
    if (project.rootProject.file('signing.properties').exists()) {
        signingProperties.load(project.rootProject.file('signing.properties').newDataInputStream())
    }

    signingConfigs {
        if (signingProperties.getProperty("storeFile") != null) {
            sign {
                storeFile file(signingProperties.getProperty("storeFile"))
                storePassword signingProperties.getProperty("storePassword")
                keyAlias signingProperties.getProperty("keyAlias")
                keyPassword signingProperties.getProperty("keyPassword")
            }
        }
    }

    buildTypes {
        debug {
            if (signingProperties.getProperty("storeFile") != null) {
                signingConfig signingConfigs.sign
            }
            jniDebuggable true
            applicationIdSuffix ".beta"
            resValue "string", "BuildAppName", "@string/AppNameBeta"
        }

        pbeta {
            minifyEnabled true
            shrinkResources true
            if (signingProperties.getProperty("storeFile") != null) {
                signingConfig signingConfigs.sign
            }
            jniDebuggable true
            proguardFiles '../TMessagesProj/proguard-rules.pro'
            ndk.debugSymbolLevel 'FULL'
            applicationIdSuffix ".beta"
            resValue "string", "BuildAppName", "@string/AppNameBeta"
        }

        release {
            minifyEnabled true
            shrinkResources true
            if (signingProperties.getProperty("storeFile") != null) {
                signingConfig signingConfigs.sign
            }
            proguardFiles '../TMessagesProj/proguard-rules.pro'
            ndk.debugSymbolLevel 'FULL'
            resValue "string", "BuildAppName", "@string/AppName"
        }

        play {
            initWith release
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def abi = output.getFilter('ABI')
            if (abi == null) abi = 'universal'
            outputFileName = "OwlGram-Huawei-${defaultConfig.versionName}-${defaultConfig.versionCode}-${abi}.apk"
            def abiVersionCode = project.ext.abiCodes.get(abi)
            output.versionCodeOverride = defaultConfig.versionCode * 10 + abiVersionCode
        }
    }

    defaultConfig {
        applicationId "it.owlgram.android"
        minSdkVersion 21
        targetSdkVersion 32
        versionName APP_VERSION_NAME
        versionCode Integer.parseInt(APP_VERSION_CODE)
        buildConfigField "String", "GIT_COMMIT_HASH", "\"${getGitHash()}\""
        buildConfigField "Long", "GIT_COMMIT_DATE", "${getGitDate()}L"

        multiDexEnabled true

        externalNativeBuild {
            cmake {
                version '3.10.2'
                arguments '-DANDROID_STL=c++_static', '-DANDROID_PLATFORM=android-21'
                var file = Paths.get("${System.getProperty("user.dir")}${File.separator}Tools${File.separator}ccache${if (org.gradle.internal.os.OperatingSystem.current().windows) ".exe" else ""}").toFile()
                if (file.exists()) {
                    println("Using ccache ${file.getAbsolutePath()}")
                    arguments += "-DANDROID_CCACHE=${file.getAbsolutePath()}"
                } else {
                    throw new Exception("ccache not found")
                }
            }
        }
    }
    bundle {
        language {
            enableSplit = false
        }
    }

    splits.abi {
        enable true
        universalApk true
    }

    dependenciesInfo.includeInApk false
    namespace 'org.telegram.messenger'
}

task("buildRelease") {
    dependsOn("assembleRelease")
    dependsOn("bundlePlay")
}
