import java.nio.file.Files
import java.nio.file.Paths

plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

configurations.all {
    exclude group: 'com.google.firebase', module: 'firebase-core'
    exclude group: 'androidx.recyclerview', module: 'recyclerview'
}

dependencies {
    implementation 'androidx.core:core:1.7.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation "androidx.sharetarget:sharetarget:1.2.0-rc01"
    implementation 'androidx.cardview:cardview:1.0.0'

    compileOnly 'org.checkerframework:checker-qual:3.22.0'
    compileOnly 'org.checkerframework:checker-compat-qual:2.5.5'
    implementation 'com.google.firebase:firebase-messaging:23.0.4'
    implementation 'com.google.firebase:firebase-config:21.1.0'
    implementation 'com.google.firebase:firebase-appindexing:20.0.0'
    implementation 'com.google.android.gms:play-services-maps:18.0.2'
    implementation 'com.google.android.gms:play-services-auth-api-phone:18.0.1'
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.android.gms:play-services-wearable:17.1.0'
    implementation 'com.google.android.gms:play-services-location:19.0.1'
    implementation 'com.google.android.gms:play-services-wallet:19.1.0'
    //noinspection GradleDependency
    implementation 'com.googlecode.mp4parser:isoparser:1.0.6' // NOT YET SUPPORTED BY TELEGRAM
    //noinspection GradleDependency
    implementation 'com.stripe:stripe-android:2.0.2' // NOT YET SUPPORTED BY TELEGRAM
    implementation 'com.google.mlkit:language-id:17.0.3'
    implementation files('libs/libgsaverification-client.aar')
    implementation 'org.apache.commons:commons-text:1.9'

    // CameraX Implementation
    implementation "androidx.camera:camera-core:1.1.0-beta03"
    implementation "androidx.camera:camera-camera2:1.1.0-beta03"
    implementation "androidx.camera:camera-lifecycle:1.1.0-beta03"
    implementation "androidx.camera:camera-view:1.1.0-beta03"
    implementation "androidx.camera:camera-extensions:1.1.0-beta03"
    implementation 'androidx.interpolator:interpolator:1.0.0'

    // GOOGLE PLAY UPDATE CHECKER
    implementation 'com.google.android.play:core:1.10.3'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}

ext.abiCodes = ['universal':0, 'armeabi-v7a':1, 'arm64-v8a':5, x86:2, x86_64:6]

android {
    compileSdkVersion 32
    buildToolsVersion '32.1.0-rc1'
    ndkVersion '21.4.7075529'

    defaultConfig.applicationId = "it.owlgram.android"

    sourceSets.main.jniLibs.srcDirs = ['./jni/']

    buildFeatures {
        prefab true
    }

    externalNativeBuild {
        cmake {
            path 'jni/CMakeLists.txt'
        }
    }

    lint {
        checkReleaseBuilds false
        disable 'MissingTranslation', 'ExtraTranslation', 'BlockedPrivateApi'
    }

    packagingOptions {
        resources {
            excludes += '**'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11

        coreLibraryDesugaringEnabled true
    }

    Properties signingProperties = new Properties()
    if (project.rootProject.file('signing.properties').exists()) {
        signingProperties.load(project.rootProject.file('signing.properties').newDataInputStream())
    }

    signingConfigs {
        if (signingProperties.getProperty("storeFile") != null) {
            sign {
                storeFile file(signingProperties.getProperty("storeFile"))
                storePassword signingProperties.getProperty("storePassword")
                keyAlias signingProperties.getProperty("keyAlias")
                keyPassword signingProperties.getProperty("keyPassword")
            }
        }
    }

    buildTypes {
        debug {
            if (signingProperties.getProperty("storeFile") != null) {
                signingConfig signingConfigs.sign
            }
            jniDebuggable true
            applicationIdSuffix ".beta"
            resValue "string", "BuildAppName", "@string/AppNameBeta"
        }

        release {
            minifyEnabled true
            shrinkResources true
            if (signingProperties.getProperty("storeFile") != null) {
                signingConfig signingConfigs.sign
            }
            proguardFiles 'proguard-rules.pro'
            ndk.debugSymbolLevel 'FULL'
            resValue "string", "BuildAppName", "@string/AppName"
        }
    }

    defaultConfig.versionCode = 2646

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def abi = output.getFilter('ABI')
            if (abi == null) abi = 'universal'
            outputFileName = "OwlGram-${defaultConfig.versionName}-${defaultConfig.versionCode}-${abi}.apk"
            def abiVersionCode = project.ext.abiCodes.get(abi)
            output.versionCodeOverride = defaultConfig.versionCode * 10 + abiVersionCode
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32
        versionName "1.9.0 Beta 1"

        externalNativeBuild {
            cmake {
                version '3.10.2'
                arguments '-DANDROID_STL=c++_static', '-DANDROID_PLATFORM=android-21', "-DNDK_CCACHE=ccache"
            }
        }
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    splits.abi {
        enable true
        universalApk true
    }

    dependenciesInfo.includeInApk false
}

def patchBundleReleaseManifest = task('patchBundleReleaseManifest').doLast {
    def manifest = Paths.get(project.buildDir.path, 'intermediates',
            'bundle_manifest', 'release', 'AndroidManifest.xml')
    def manifestContent = manifest.getText()
    manifestContent = manifestContent.replace("owlgram.apks", "owlgram.bundle")
    Files.write(manifest, manifestContent.getBytes())
}

afterEvaluate {
    tasks.getByName("processApplicationManifestReleaseForBundle").finalizedBy patchBundleReleaseManifest
}