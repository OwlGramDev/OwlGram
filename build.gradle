// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://developer.huawei.com/repo/' }
    }
    dependencies {
        classpath group: 'com.huawei.agconnect', name: 'agcp', version: '1.6.0.300'
    }
}

plugins {
    id 'com.android.application' version '7.3.1' apply false
    id 'com.google.gms.google-services' version '4.3.10' apply false
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin' version '2.0.1' apply false
}

subprojects {
    afterEvaluate {
        if (plugins.hasPlugin('com.android.library') ||
                plugins.hasPlugin('com.android.application')) {
            android {
                compileSdkVersion 33
                buildToolsVersion '33.0.0'
                ndkVersion '21.4.7075529'

                lint {
                    checkReleaseBuilds false
                    disable 'MissingTranslation', 'ExtraTranslation', 'BlockedPrivateApi'
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_11
                    targetCompatibility JavaVersion.VERSION_11

                    coreLibraryDesugaringEnabled true
                }

                defaultConfig {
                    minSdkVersion 21
                    targetSdkVersion 33
                }

                buildTypes {
                    play {
                        initWith release
                    }
                }
            }

            dependencies {
                //noinspection GradleDependency
                coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2' // NEEDED TESTS OF THE NEW R8 VERSION
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}